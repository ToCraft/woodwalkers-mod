loom {
    accessWidenerPath = file("src/main/resources/walkers.accesswidener")
}


dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"

    // APIs
    modApi "dev.architectury:architectury:${rootProject.architectury_version}"
    modApi "dev.tocraft:craftedcore:${rootProject.minecraft_version}-${rootProject.craftedcore_version}"
}

architectury {
    common("fabric", "forge", "neoforge")
}

configurations {
    dev
}

artifacts {
    dev(jar)
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifactId = rootProject.archivesBaseName
            version = rootProject.minecraft_version + "-" + rootProject.mod_version
            from components.java
        }
    }
    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                name = "tocraftMavenPublic"
                url "https://maven.tocraft.dev/public"

                credentials {
                    username = "tocraft"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}